## AnnaDB 0.2 Release Notes

### New Features

* `project{...}` step to modify the output data

### Improvements

* Overall speed improvements

## New features input output examples

### Project

The `project{...}` step allows you to modify the output data. For example, you can keep onl the fields you need.

Original data:

```tyson
v[
    m{
      s|name|: s|John|,
      s|age|: 30,
      s|address|: m{
        s|street|: s|Main Street|,
        s|city|: s|New York|,
      },
      s|emails|: l[s|john@gmail.com|, s|john_ny@outlook.com|]
    },
    m{
      s|name|: s|Mary|,
      s|age|: 25,
      s|address|: m{
        s|street|: s|Main Street|,
        s|city|: s|New York|,
      },
      s|emails|: l[s|mary@gmail.com|, s|mary_25@outlook.com|]
    },
]
```

Input
```tyson
collection|users|:q[
    find[],
    sort[asc(value|name|)],
    project{
        s|name|:keep,
        s|age|:keep,
    }
]
```

Output
```tyson
ok[
    response{
        s|data|:objects{
            test|b32cb267-e102-43f1-8254-15ab50266c2e|:m{
                s|name|:s|John|,
                s|age|:n|30|,
            },
            test|dbc87152-918d-497b-b379-378955bac7dd|:m{
                s|name|:s|Mary|,
                s|age|:n|25|,
            },
        },
        s|meta|:find_meta{
            s|count|:n|2|,
        },
    },
]
```

Or you can set a new field using values from other:

Input
```tyson
collection|users|:q[
    find[],
    sort[asc(value|name|)],
    project{
        s|title|:value|name|
    }
]
```

Output
```tyson
ok[
    response{
        s|data|:objects{
            test|b32cb267-e102-43f1-8254-15ab50266c2e|:m{
                s|title|:s|John|,
            },
            test|dbc87152-918d-497b-b379-378955bac7dd|:m{
                s|title|:s|Mary|,
            },
        },
        s|meta|:find_meta{
            s|count|:n|2|,
        },
    },
]

```

Or you can set a primitive value for the field:

Input
```tyson
collection|users|:q[
    find[],
    sort[asc(value|name|)],
    project{
        s|name|:s|NEW VALUE|
    }
]
```

Output
```tyson
ok[
    response{
        s|data|:objects{
            test|b32cb267-e102-43f1-8254-15ab50266c2e|:m{
                s|name|:s|NEW VALUE|,
            },
            test|dbc87152-918d-497b-b379-378955bac7dd|:m{
                s|name|:s|NEW VALUE|,
            },
        },
        s|meta|:find_meta{
            s|count|:n|2|,
        },
    },
]
```

Or you can set a map or vector as a field value using previously mentioned tools like `keep` or `value|...|`:

Input for map
```tyson
collection|users|:q[
    find[],
    sort[asc(value|name|)],
    project{
        s|name|:m{
            s|title|:value|name|,
        },
        s|address|:m{
            s|street|:keep
        }
    }
]
```

Output
```tyson
ok[
    response{
        s|data|:objects{
            test|b32cb267-e102-43f1-8254-15ab50266c2e|:m{
                s|name|:m{
                    s|title|:s|John|,
                },
                s|address|:m{
                    s|street|:s|Main Street|,
                },
            },
            test|dbc87152-918d-497b-b379-378955bac7dd|:m{
                s|name|:m{
                    s|title|:s|Mary|,
                },
                s|address|:m{
                    s|street|:s|Main Street|,
                },
            },
        },
        s|meta|:find_meta{
            s|count|:n|2|,
        },
    },
]
```

Input for vector
```tyson
collection|users|:q[
    find[],
    sort[asc(value|name|)],
    project{
        s|name|:v[
            value|name|,
        ],
        s|emails|:v[s|TEST|,keep]
    }
]
```


Output
```tyson
ok[
    response{
        s|data|:objects{
            test|b32cb267-e102-43f1-8254-15ab50266c2e|:m{
                s|name|:v[
                    s|John|,
                ],
                s|emails|:v[
                    s|TEST|,
                    s|john_25@outlook.com|
                ]
            },
            test|dbc87152-918d-497b-b379-378955bac7dd|:m{
                s|name|:v[
                    s|Mary|,
                ],
                s|emails|:v[
                    s|TEST|,
                    s|mary_25@outlook.com|
                ],
            },
        },
        s|meta|:find_meta{
            s|count|:n|2|,
        },
    },
]
```